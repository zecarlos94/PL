program -> declarations 'START' body 'END'

declarations -> EMPTY
              | declarations variavel ';'
              | declarations variavel '=' atributions ';'

body -> EMPTY
      | body statement

statement -> atribution ';'
           | flowControl 
           | io ';'
           | functionCall ';'

io -> 'read' '(' variavel ')'
    | 'print' '(' prints ')'

prints -> value
        | '"' TEXT '"' 

atribution -> variavel '=' atributions

atributions -> expression
             | functionCall

flowControl -> 'while' '(' booleanExpression ')' '{' body '}'
             | 'if' '(' booleanExpression ')' '{' body '}' else 

else -> 'else' '{' body '}'
      | EMPTY

expression -> parcel
            | expression '+' parcel 
            | expression '-' parcel

parcel -> parcel '*' factor
        | parcel '/' factor
        | parcel '%' factor
        | factor

factor -> value
        | '(' expression ')'

booleanExpression -> booleanExpression '&&' booleanFactor
                   | booleanExpression '||' booleanFactor
                   | booleanFactor

booleanFactor -> expression booleanSignal expression
               | '(' booleanExpression ')'

booleanSignal -> '=='
               | '!='
               | '<'
               | '>'
               | '<='
               | '>='

functionCall -> NAME '(' args ')'

args -> moreArgs
      | EMPTY

moreArgs -> value 
          | moreArgs ',' value 

value -> NUMBER
       | variavel

variavel -> NAME
          | NAME '[' expression ']'
          | NAME '[' expression ']' '[' expression ']'
